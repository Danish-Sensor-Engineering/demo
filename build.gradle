plugins {
    id 'java'
    id 'groovy'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'com.google.osdetector' version '1.7.3'
    id 'org.beryx.jlink' version '3.0.1'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://jitpack.io' }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'dse.explorer'
    mainClass = 'dse.explorer.Main'
}

java {
    modularity.inferModulePath.set(false)
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

javafx {
    version = '21.0.1'
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.graphics' ]
}

dependencies {
    testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'
    testImplementation 'org.slf4j:slf4j-simple:2.0.11'
    implementation 'org.slf4j:slf4j-api:2.0.11'
    implementation 'org.slf4j:slf4j-simple:2.0.11'
    implementation "javax.annotation:javax.annotation-api:1.3.2"                    // Used by afterburner.fx
    implementation 'io.github.mkpaz:atlantafx-base:2.0.1'
    implementation 'com.fazecast:jSerialComm:[2.0.0,3.0.0)'
    implementation 'com.github.Danish-Sensor-Engineering:libsensor:v2.+'
    //implementation 'dse.libs:libsensor:2.+'
}

test {
    useJUnitPlatform()
}

jlink {

    forceMerge 'org.slf4j'
    forceMerge 'com.fazecast.jSerialComm'

    options = [
            '--strip-debug',
            '--compress', '2',
            '--no-header-files',
            '--no-man-pages'
    ]
    launcher {
        name = 'ODS Explorer'
        noConsole = true
    }

    // Only works from Java 14+
    jpackage {
        imageName = "ODS-Explorer"
        skipInstaller = true
        installerName = "ods-explorer-${osdetector.arch}"
        installerOptions += [
                '--vendor', 'Danish Sensor Engineering',
                '--description', 'Danish Sensor Engineering - ODS Explorer',
                '--copyright', 'Danish Sensor Engineering ApS',
                '--app-version', version
        ]

        // Requires: https://wixtoolset.org/ to create installer on Windows
        if(osdetector.os == 'windows') {
            installerType = 'msi'
            skipInstaller = false
            installerOptions += [
                    '--win-per-user-install',
                    '--win-dir-chooser',
                    '--win-menu',
                    //'--icon', 'src/main/resources/icon.png'
            ]
        }

        // Requires: xcode-select --install
        if(osdetector.os == 'osx') {
            installerType = 'dmg'
            skipInstaller = false
            installerOptions += [
                    //'--icon', 'src/main/resources/icon.icns'
            ]
        }

        // Requires: build-rpm / rpm and dpkg-dev
        if(osdetector.os == 'linux') {
            skipInstaller = false
            installerOptions += [
                    '--linux-shortcut',
                    '--linux-menu-group', 'Internet',
                    '--linux-rpm-license-type', 'APACHE-20',
                    '--linux-deb-maintainer', 'mark.nellemann@gmail.com',
                    //'--icon', 'src/main/resources/icon.png',
            ]
        }

    }

}